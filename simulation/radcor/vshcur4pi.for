c=========================================================================
c              Общие подпрограммы для программ
c        вычисления матричного элемента в процессах ee->4pi
c========================================================================
       SUBROUTINE A1P1(p1,p2,p3,p4,ta1)

C++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C                data 18.02.99 (Alexey Berdyugin)
C++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

       implicit none

       integer  i

       real  p1(0:3),p2(0:3),p3(0:3),p4(0:3)
       real  E,p4p3,p4p1,c1,c2,c3,c4,FmA1

       real s04
       real*8 s13

       complex*16 c0,DpA1,DpRho,ta1(3)

C++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

       E = p1(0)+p2(0)+p3(0)+p4(0)

       s04 = (E-p4(0))**2 - p4(1)**2 - p4(2)**2 - p4(3)**2

       if(s04.lt.0) then
         write(6,*) 'VSH004:  !!! Argument of DpA1 <0 !!!'
         s04=1.0E-6
       end if
       
       s13=(p1(0)+p3(0))**2-(p1(1)+p3(1))**2-
     &   (p1(2)+p3(2))**2-(p1(3)+p3(3))**2
       p4p3=p4(0)*p3(0)-p4(1)*p3(1)-p4(2)*p3(2)-p4(3)*p3(3)
       p4p1=p4(0)*p1(0)-p4(1)*p1(1)-p4(2)*p1(2)-p4(3)*p1(3)
       c0=(FmA1(s04))**2/(DpA1(s04)*DpRho(s13))
       c1=E-p4(0)
       c2=E*p3(0)-p4p3
       c3=E*p1(0)-p4p1
       c4=p1(0)*p4p3-p3(0)*p4p1
       do 1 i=1,3
 1       ta1(i)=c0*(c1*(p1(i)*c2-p3(i)*c3)-p4(i)*c4)
C++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

       RETURN
       END



c===================================================================
       
       SUBROUTINE A1SP1(p1,p2,p3,p4,ta1)

       implicit none

       integer  i

       real  p1(0:3),p2(0:3),p3(0:3),p4(0:3)
       real  E,p4p3,p4p1,c1,c2,c3,c4,FmA1

       real s04
       real s13

       complex*16 c0,DpA1,DpSigma,ta1(3)

c------------------------------------------------------------------------

       E = p1(0)+p2(0)+p3(0)+p4(0)

       s04 = (E-p4(0))**2 - p4(1)**2 - p4(2)**2 - p4(3)**2

       if(s04.lt.0) then
         write(6,*) 'VSH004:  !!! Argument of DpA1S <0 !!!'
         s04=1.0E-6
       end if
       
       s13=(p1(0)+p3(0))**2-(p1(1)+p3(1))**2-
     &   (p1(2)+p3(2))**2-(p1(3)+p3(3))**2

       c0=(FmA1(s04))**2/(DpA1(s04)*DpSigma(s13))
       c1=E-p4(0)

       do i=1,3
         ta1(i)=c0*s04*(c1*p2(i)+p2(0)*p4(i))
       end do
       

       RETURN
       END


c===================================================================
c          подпрограмма вычисления компонентов тока J_h1
c               автор Вячеслав Шарый 
c                  февраль 2000
c===================================================================
       
       SUBROUTINE H1P1(p1,p2,p3,p4,th1)

       implicit none

       integer  i

       real  p1(0:3),p2(0:3),p3(0:3),p4(0:3)
       real  E,p4p3,p4p1,c1,c2,c3,c4,FmH1

       real s04
       real*8 s13

       complex*16 c0,DpH1,DpRho,th1(3)

c--------------------------------------------------------------------

       FmH1=1.

       E = p1(0)+p2(0)+p3(0)+p4(0)

       s04 = (E-p4(0))**2 - p4(1)**2 - p4(2)**2 - p4(3)**2

       if(s04.lt.0) then
         write(6,*) 'VSH004:  !!! Argument of DpH1 <0 !!!'
         s04=1.0E-6
       end if

       s13 = (p1(0)+p3(0))**2 - (p1(1)+p3(1))**2 -
     &   (p1(2)+p3(2))**2 - (p1(3)+p3(3))**2

       p4p3 = p4(0)*p3(0)-p4(1)*p3(1)-p4(2)*p3(2)-p4(3)*p3(3)

       p4p1 = p4(0)*p1(0)-p4(1)*p1(1)-p4(2)*p1(2)-p4(3)*p1(3)

       c0 = FmH1**2/(DpH1(s04)*DpRho(s13))
       c1=E-p4(0)
       c2=E*p3(0)-p4p3
       c3=E*p1(0)-p4p1
       c4=p1(0)*p4p3-p3(0)*p4p1

       do 1 i=1,3
 1       th1(i)=c0*(c1*(p1(i)*c2-p3(i)*c3)-p4(i)*c4)

c--------------------------------------------------------------------

       RETURN
       END

c===================================================================
c          подпрограмма вычисления компонентов тока J_\pi(1300)
c               автор Вячеслав Шарый 
c                  февраль 2000
c===================================================================
       
       SUBROUTINE PP1(p1,p2,p3,p4,tp)

       implicit none

       integer  i

       real  p1(0:3),p2(0:3),p3(0:3),p4(0:3)
       real  E,p4p3,p2p3,Fmp

       real s01
       real*8 s24

       complex*16 c0,DpPiPrime,DpRho,tp(3)

c--------------------------------------------------------------------

       Fmp=1.

       E = p1(0)+p2(0)+p3(0)+p4(0)

       s01 = (E-p1(0))**2 - p1(1)**2 - p1(2)**2 - p1(3)**2

       if(s01.lt.0) then
         write(6,*) 'VSH004:  !!! Argument of DpPiPrime <0 !!!'
         s01=1.0E-6
       end if

       s24 = (p2(0)+p4(0))**2 - (p2(1)+p4(1))**2 -
     &   (p2(2)+p4(2))**2 - (p2(3)+p4(3))**2

       p4p3 = p4(0)*p3(0)-p4(1)*p3(1)-p4(2)*p3(2)-p4(3)*p3(3)

       p2p3 = p2(0)*p3(0)-p2(1)*p3(1)-p2(2)*p3(2)-p2(3)*p3(3)

       c0 = Fmp**2/(DpPiPrime(s01)*DpRho(s24))

       do i=1,3
         tp(i)=c0*(p2p3-p4p3)*s24*p1(i)
       end do
       
c--------------------------------------------------------------------

       RETURN
       END

c===================================================================
c          подпрограмма вычисления компонентов тока J_\sigma
c               автор Вячеслав Шарый 
c                  февраль 2000
c===================================================================
       
       SUBROUTINE SigP1(p1,p2,p3,p4,tsig)

       implicit none

       integer  i

       real  p1(0:3),p2(0:3),p3(0:3),p4(0:3)
       real  E,FmSig

       real*8  s14
       real   s23

       complex*16 c0,DpSigma,DpRho,tsig(3)

c--------------------------------------------------------------------

       FmSig=1.

       s23 = (p2(0)+p3(0))**2 - (p2(1)+p3(1))**2 -
     &   (p2(2)+p3(2))**2 - (p2(3)+p3(3))**2

       s14 = (p1(0)+p4(0))**2 - (p1(1)+p4(1))**2 -
     &   (p1(2)+p4(2))**2 - (p1(3)+p4(3))**2

       c0 = FmSig**2/(DpSigma(s23)*DpRho(s14))

       do i=1,3
         tsig(i)=c0*(p4(0)*p1(i)-p1(0)*p4(i))
       end do
       
c--------------------------------------------------------------------

       RETURN
       END

c=================================================================
c      Зависимость ширины \sigma мезона от энергии
c=================================================================
      
      double precision FUNCTION wsigma(s)

      implicit none

      double precision ms2
      parameter ( ms2 = 139.57**2 ) ! масса заряженного пи мезона в квадрате
      double precision ms
      data ms /ms2/
      save ms

      real s

      if (s .le. 4*ms) then
        wsigma = 0.
      else
        wsigma = sqrt(1 - 4*ms/s)
      end if
      
      RETURN
      END

c==================================================================
c      Инверсный пропогатор \sigma мезона
c=================================================================
      
      COMPLEX*16 FUNCTION DpSigma(s)

      implicit none

      real msig /800.0/ ! масса sigma мезона 
      real gsig /800./ ! ширина sigma мезона 

      real s, E2

      double precision wsigma

       E2=sqrt(S)

       DpSigma=dcmplx(Msig**2-S,-(gsig*msig*wsigma(s)/wsigma(msig**2)))
      
      RETURN

      END

c===================================================================
c          подпрограмма вычисления компонентов тока J_{a_2}
c               автор Вячеслав Шарый 
c                  февраль 2000
c===================================================================
       
      SUBROUTINE A2P1(p1,p2,p3,p4,ta2)

      implicit none

      double precision ma2s
      parameter (ma2s=1.318d3**2)

       integer  i

       real  p1(0:3), p2(0:3), p3(0:3), p4(0:3)
       real  p4p2v(1:3), p1p3v(1:3), p132v(1:3)
       real  E, Fma2, p4p1, p4p2, p4p3, p2q, p4q

       real  s04,  c1, c2
       real*8 s13

       complex*16 c0, Dpa2, DpRho, ta2(3)

c--------------------------------------------------------------------

       Fma2=1.

       E = p1(0)+p2(0)+p3(0)+p4(0)

       s04 = (E-p4(0))**2 - p4(1)**2 - p4(2)**2 - p4(3)**2

       if(s04.lt.0) then
         write(6,*) 'VSH004:  !!! Argument of DpA2 <0 !!!'
         s04=1.0E-6
       end if

       s13 = (p1(0)+p3(0))**2 - (p1(1)+p3(1))**2 -
     &   (p1(2)+p3(2))**2 - (p1(3)+p3(3))**2

       p4p1 = p4(1)*p1(1)+p4(2)*p1(2)+p4(3)*p1(3)
       p4p2 = p4(1)*p2(1)+p4(2)*p2(2)+p4(3)*p2(3)
       p4p3 = p4(1)*p3(1)+p4(2)*p3(2)+p4(3)*p3(3)

       p2q = (E-p4(0))*p2(0)+p4(1)*p2(1)+p4(2)*p2(2)+p4(3)*p2(3)
       p4q = (E-p4(0))*p4(0)+p4(1)*p4(1)+p4(2)*p4(2)+p4(3)*p4(3)

       p4p2v(1) =  p4(1)*p2(2) - p4(2)*p2(1)
       p4p2v(2) = -p4(2)*p2(3) + p4(3)*p2(2)
       p4p2v(3) =  p4(3)*p2(1) - p4(1)*p2(3)

       p1p3v(1) =  p1(1)*p3(2) - p1(2)*p3(1)
       p1p3v(2) = -p1(2)*p3(3) + p1(3)*p3(2)
       p1p3v(3) =  p1(3)*p3(1) - p1(1)*p3(3)
       
       p132v(1) =  p1p3v(1)*p2(2) - p1p3v(2)*p2(1)
       p132v(2) = -p1p3v(2)*p2(3) + p1p3v(3)*p2(2)
       p132v(3) =  p1p3v(3)*p2(1) - p1p3v(1)*p2(3)

       c1 = p4p2v(1)*p132v(1)+p4p2v(2)*p132v(2)+p4p2v(3)*p132v(3)

       c2 = p2(0)*p4(0) - p4p2 - p2q*p4q/ma2s
       
       c0 = Fma2**2/(Dpa2(s04)*DpRho(s13))

       do i=1,3
         ta2(i)=c0*( E*c1 + c2*(
     &     p1(i)*(p3(0)*p4p2 - p2(0)*p4p3) -
     &     p3(i)*(p1(0)*p4p2 - p2(0)*p4p1) -
     &     p2(i)*(p1(0)*p4p3 - p3(0)*p4p1) ))
       end do
       
c--------------------------------------------------------------------

       RETURN
       END

c===================================================================
c          подпрограмма вычисления компонентов тока J_{om}
c               автор Вячеслав Шарый 
c                  апрель 2000
c===================================================================
       
      SUBROUTINE OMP1(p1,p2,p3,p4,tom)

       implicit none
 
       integer  i

       include 'fitdata.inc'

       real  p1(0:3), p2(0:3), p3(0:3), p4(0:3)
       real  E, Fmom, p1p4, p1p2, p1p3

       real   c1, c2
       real*8 s34, s01

       complex*16 c0, Dpom, DpRho, tom(3)

       real lso
       common/OMFM/ lso

c--------------------------------------------------------------------

       E = p1(0)+p2(0)+p3(0)+p4(0)

       s01 = (E-p1(0))**2 - p1(1)**2 - p1(2)**2 - p1(3)**2

       if(s01.lt.0) then
         write(6,*) 'VSH004:  !!! Argument of Dpom <0 !!!'
         s01=1.0E-6
       end if


       if(lso.eq.0.)then
         FmOm = 1.
       else
         FmOm = ((1+MsOm**2/lso)/(1+s01/lso))
       end if


       s34 = (p3(0)+p4(0))**2 - (p3(1)+p4(1))**2 -
     &   (p3(2)+p4(2))**2 - (p3(3)+p4(3))**2

       p1p2 = p1(1)*p2(1)+p1(2)*p2(2)+p1(3)*p2(3)
       p1p3 = p1(1)*p3(1)+p1(2)*p3(2)+p1(3)*p3(3)
       p1p4 = p1(1)*p4(1)+p1(2)*p3(2)+p1(3)*p4(3)

       c0 = Fmom**2/(Dpom(s01)*DpRho(s34))

       do i=1,3
         
         tom(i)=c0*( (p4(0)*p3(i)-p3(0)*p4(i))*p1p2 -
     &     p2(i)*(p4(0)*p1p3-p3(0)*p1p4) -
     &     p2(0)*(p3(i)*p1p4-p4(i)*p1p3) )

       end do
       
c--------------------------------------------------------------------

       RETURN
       END
